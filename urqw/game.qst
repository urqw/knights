:1
invkill
perkill

lock_field1=0
lock_field2=0
lock_field3=0

hide_use_Зеркало_Положить=1

fedup=10
uptime=25
health_granted=35
tidyness=25
patience=15
letter=0
phoned=0
hide_use_Тамагочи_Статистика_вкл-выкл=1

pln <h1 style="font-size: 0px; font-color: #fffff">Рыцари математики</h1>
image="data/img/logo.png"
btn brief, Играть
btn about, Об игре
end

:brief
music data/mus/LullabyMB.mp3
pln <h2>Что происходит</h2>
pln Майгородский технический институт — место, где многие мечтают учиться, а вы — учитесь. В экспериментальный исследовательский центр при институте съезжаются студенты-математики, физики и биоинформатики со всего мира: поучаствовать в конференциях и поработать в местной лаборатории. Здесь развивают проекты, на которые в других местах давно махнули бы рукой.
pln 
pln Институтский городок в 20 км от станции Майгород — тихая идиллия. Загородный воздух, двухэтажные домики с кофейного цвета крышами и «молочными» стенами — для студентов и персонала. Свой кинотеатр. Футуристическое здание центра обработки данных. Но что за жизнь без приключений? 
pln
pln К концу первого семестра вы поняли, что быть в гуще здешней жизни можно, только став частью студенческого Подполья. В институте о нем все знают, но не все туда могут вступить. Кандидаты проходят испытание, придуманное «загадайкой». Это ежегодно сменяемый лидер Подполья, который перед выпуском из института cоставляет головоломки для новичков. Вы решили, что пройдёте огонь, воду и медные трубы, но проникнете в тайное студенческое общество.
btn instructions, Вперёд!
end

:about
pln <h2>Об игре</h2>
pln
pln Игра переиздана к своему 10-летию. Первая публикация состоялась 10.02.2009.
pln Версия 2019 стабильно работает в интерпретаторе UrqW 1.0.
pln Полный список отличий от прошлой версии см. в Readme.
pln
pln <h3>Авторство</h3>
pln 
pln Идея, оформление, код: Мария Лис (Lemurqa)
pln lemon.multilab@gmail.com
pln 
pln Главная музыкальная тема создана с помощью приложения ACS и сервиса UJam Studio.
pln В игре также звучит фокстрот <a href="https://soundcloud.com/ladrones-de-gallinas/fax-trot-del-rosquero" target="blank">"Fax Trot Del Rosquero" в исполнении Ladrones de Gallinas</a>.
btn 1, В главное меню
end

:instructions
image="data/img/Shturmanov.png"
pln </br><strong>Гриня «Штурман»</strong>, подпольщик-третьекурсник с биоинформатики, говорит: 
pln — У тебя день на всё. Дольше нам трудно держать подсказки на местах. Чем быстрее справишься, тем лучше. Зеркало есть?
pln — А?
pln — Вот. Положи куда-нибудь. — протягивает вам карманное зеркало. — Будь здоров.
pln — Погоди! А задание? Ты ничего не сказал толком.
pln — Говорить-то нечего. Сам додумаешься.
pln — Оригинально! Хм...
pln — Задание в этом году совсем простое. У нас посложнее было.
pln Гриня весело улыбнулся и пошел в сторону вестибюля.
inv+ Зеркало
btn firstfloor, Приступить
end

:firstfloor
if patience=0 then goto eend
pln <h3>Коридор</h3>
pln Вы стоите посреди шумного коридора, мимо ходят студенты и преподаватели. Из окон льёт свет зимнего утра.
just=1
btn stands, В вестибюль
btn phone, Дальше по коридору
btn lib, В библиотеку
end

:canteen
if patience=0 then goto eend
if inv_Тамагочи=1 then cls
if pin_mode_on=0 and egg_stats_on=1 then proc stats_bar
if hungry=1 and pin_mode_on=0 then pln <strong><i>[Тамагочи] Медведь голоден.</i></strong></br></br>
if dirty=1 and pin_mode_on=0 then pln <strong><i>[Тамагочи] Пора убирать за питомцем.</i></strong></br></br>
if egg_activated=1 and asleep=0 and tidyness>0 then tidyness=tidyness-1
if tidyness=0 and dirty=0 and pin_mode_on=0 then dirty=1 & pln <i>Тамагочи пищит! Нужна уборка.</i></br></br>
if ill=1 and pin_mode_on=0 then pln <strong><i>[Тамагочи] Мишка болеет.</i></strong></br></br>
pln <h3>Столовая</h3>
pln Вы в столовой. Впереди — стойки, где накладывают еду. Там постоянно крутятся голодные ребята с тарелками. Справа от вас — круглые столики, за которыми едят и разговаривают несколько человек. 
btn bar, К стойкам с едой
btn dining_tables, К столикам
btn phone, В коридор
end

:dining_tables
if patience=0 then goto eend
if pre_win=0 or inv_Тамагочи=1 then p Стены в этой части зала украшены разноцветными панно с видами Майгорода. Летние пейзажи, зарисовки поселения, которое было здесь в 1880-е, изображение старинной водонапорной башни, что уцелела в городке до наших дней.</br></br>Ещё не занятые столики радуют чистотой. Но рассиживаться некогда. & btn canteen, Назад
if egg_collected=0 and pre_win=1 then p За одним из столов вы замечаете знакомое лицо — это аспирант Валерий Кижуч. Неделю назад он вёл у вас на потоке семинар по матанализу.</br></br> & image="data/img/Aspirant.png" & p </br></br>О! Кижуч заметил ваш взгляд и приветственно кивнул. Он встаёт и идёт к вам. & egg_collected=1 & btn messenger, Поздороваться
end

:bar
if patience=0 then goto eend
pln <h3>У прилавка</h3> 
if jbeen_here=0 then p Вы подходите к витринам-холодильникам. & jbeen_here=1 else p Вы у прилавка. 
pln Салаты, нарезки всякие, булочки, напитки... Всё как всегда. 
if pre_win=1 and asked_the_price=0 then pln На ценнике к яблокам, вместо цены в привычном формате, стоит цифра «3». & btn ask, Спросить про ценник
if pre_win=1 then pln Из динамика, висящего у кассы, доносится рекламная песенка (к счастью, негромко!). Куплеты рассказывают о пользе правильного питания, а припев какой-то дикий: & pln & proc hint_song & btn listen_more, Слушать ещё
btn jbeen_here=0 & cls & goto canteen, Назад
end

:listen_more
if patience=0 then goto eend
jbeen_here=1
pln <p>Вы снова обращаете внимание на припев:</p></br></br>
proc hint_song
btn listen_more, Слушать ещё
btn bar, Назад
end

:ask
if patience=0 then goto eend
asked_the_price=1
pln Вы обращаетесь к рыженькой девушке-кассиру:
pln — Здравствуйте! Скажите, у вас яблоки по три рубля, что ли?
pln — А? Извините, не поняла. Почему по три?
pln — Вот, на ценнике тройка.
pln — Где? — (ищет) — ...Ой. Это не ценник. Даже не знаю, откуда это. — забирает бумажку — Яблоки по 12 рублей.
btn bar, Ясно
end

:hint_song
ref=-1
if heard=1 then ref=rnd4 & if prev_ref=ref then goto hint_song

if ref=1 then pln <i>И в самом начале банкета</br>Они заказали подряд</br>два супа, четыре котлеты,</br>три чая, один шоколад.</i> & prev_ref=ref

if ref=2 then pln <i>И нет никакого секрета,</br>что ждут в перерыве ребят</br>два супа, четыре котлеты,</br>три чая, один шоколад!».</i> & prev_ref=ref

if ref=3 then pln <i>Ты ей закажи два букета,</br>Вино, ветчину и салат,</br>пломбира четыре брикета,</br>три сока, один лимонад.</i> & prev_ref=ref

if heard=0 or ref=4 then pln <i>Приходят Роман и Джульетта</br>в столовую и говорят:</br>«Два супа, четыре котлеты,</br>три чая, один шоколад».</i> & heard=1 & prev_ref=ref
end

:messenger
pln <strong>Аспирант:</strong> На ловца и зверь бежит! Вам просили передать вот это.
pln Он достаёт из кармана пиджака и протягивает сиреневый брелок с экранчиком и кнопками. Да это ж тамагочи!
pln — Извините... Кто просил? Это точно мне?
pln — Просили кураторы вашего... Испытания или как правильно... Устроили детский сад какой-то.
pln Вы берёте брелок и мельком смотрите на экран — пусто. 
pln — Хорошего дня. — говорит аспирант и направляется к выходу из столовой.
pln — Спасибо!
btn inv+ Тамагочи & goto canteen, Убрать брелок в карман
end

:phone
if patience=0 then goto eend
busy=0
pln <h3>Коридор. Тупик</h3>
pln Впереди так называемый «телефонный тупик». На стене, в которую упирается коридор, слева висит ярко-желтый телефон. Справа у той же стены, в углу, стоит массивный справочный терминал. Студенты прозвали его конечным автоматом.
pln
pln На подходе к «тупику» — широкие деревянные двери в столовую. Они открыты.
btn terminal, К терминалу
btn telephone, К телефону
btn canteen, В столовую
btn firstfloor, Назад по коридору
end

:terminal
if patience=0 then goto eend
pln Вы подошли к терминалу, похожему на банкомат. На заставке — логотип института, под экраном — клавиатура, рядом — щель для студкарты. С помощью студенческой карточки можно и в столовой расплатиться, и получить доступ к конфиденциальной информации.
btn card, Вставить карту
btn phone, Назад
end

:card
if patience=0 then goto eend

if inv_Карточка=0 then pln Вы оставили свою карту в общежитии — всё утро думали о предстоящем испытании. Теперь идти за ней некогда.

if inv_Карточка=1 then pln Вы вставляете карту в щель терминала. После загрузки появляется панель управления, но тут же выскакивает окошко с запросом кода активации. Поле ввода разбито на три ячейки — по 7 цифр в каждой.</br></br>Над каждой ячейкой — маленькая пиктограмма. Первая изображает телефон, вторая — компьютер, третья — лист бумаги и карандаш. & btn code_activation, Ввести код
btn phone, Назад
end

:code_activation
if patience=0 then goto eend
if pre_win=1 then pln Тут уже всё готово. Нужно найти дистанционный пульт и что-то с ним сделать. else pln Перед вами три ячейки для набора кода. & btn field1, Ввести первые семь цифр & btn field2, Ввести вторые семь цифр & btn field3, Ввести последние семь цифр & btn go, Активировать карту
btn phone, Забрать карту
end

:locked_field
if patience=0 then goto eend
pln Эта ячейка уже заполнена верно. Лучше не трогать.
btn code_activation, Хорошо
end

:field1
if patience=0 then goto eend
if lock_field1=1 then goto locked_field
pln Вы помещаете курсор в ячейку с пиктограммой телефона. Можно вводить цифры.
input numbers1
proc local_check
btn code_activation, Далее
end

:field2
if patience=0 then goto eend
if lock_field2=1 then goto locked_field
pln Вы помещаете курсор в ячейку, над которой изображён компьютер. Можно вводить цифры.
input numbers2
proc local_check
btn code_activation, Далее
end

:field3
if patience=0 then goto eend
if lock_field3=1 then goto locked_field
pln Вы помещаете курсор в ячейку с пиктограммой «лист бумаги и карандаш». Можно вводить цифры.
input numbers3
proc local_check
btn code_activation, Далее
end

:local_check
if patience=0 then goto eend
if (current_loc="field3" and numbers3=2491500) or (current_loc="field2" and numbers2=1753924) or (current_loc="field1" and numbers1=5832146) then pln & pln Поле подсветилось зеленым! Похоже, эта часть кода верна.
if current_loc="field3" and numbers3=2491500 then lock_field3=1
if current_loc="field2" and numbers2=1753924 then lock_field2=1
if current_loc="field1" and numbers1=5832146 then lock_field1=1
end

:go
if patience=0 then goto eend
if numbers1=5832146 and numbers2=1753924 and numbers3=2491500 then pre_win=1 & pln На экран выскакивает окошечко с текстом: <h2>Поздравляем!</h2> Вы прошли основную часть испытания и приняты в студенческое подполье.</br></br><strong>ГИП-ГИП УРА НАШЕМУ НОВОМУ ДРУГУ!</strong></br></br>Вытрите слёзы радости — осталась одна формальность. Подтвердите активацию этой учётной записи с дистанционного пульта. Помните, теперь вы не просто студент, а подпольщик: берегите своё честное имя и не посрамите нашу организацию.  & btn phone, Вытащить карту else pln Неверно или не полностью введён код! Дальнейшая загрузка данных невозможна. & btn code_activation, Попробовать ещё раз
end

:win
invkill
music data/mus/Ladrones_de_Gallinas__FaxTrotDelRosquero.mp3
save win
pln <h1>Свершилось!</h1>
pln Итак, вы стали частью подполья и в тот же вечер прошли обряд посвящения:
pln * зачитали страницу студенческого кодекса в ритм каватины Фигаро, 
pln * получили именную печать для торжественных собраний, 
pln * выпили кружку бузы на брудершафт с полосатым динозавром — талисманом институтской футбольной команды,
pln * долго отплясывали под студенческий мини-оркестр. 
pln 
pln Наутро вы проснулись растерянным, а когда пошли на занятия, в коридоре вас окликнула сокурсница, статная и кудрявая Маргарита Коробова.
btn margo_intro, — А?
end

:margo_intro
image="data/img/Margo.png"
pln <strong>Рита</strong>: Привет! Ты вчера отлично танцевал на вечеринке после церемонии.
pln — Ты там была?!
pln — Дружочек, там было всё подполье! Но сейчас послушай три секунды. У меня к тебе большая и необычная просьба. Только не пугайся. Срочно нужен партнёр на соревнования по бальным танцам. Мы готовились с Антоном, но он вчера отметил твой успех — вывихнул ногу на вечеринке. У тебя хорошие данные. Шаги я покажу: подрабатываю уроками и объяснять таким как ты, зелёненьким, умею. Выступление вечером и...
pln — (вытаращиваете глаза) 
pln — Надо хоть как-то выступить, чтобы не вылететь с турнира. К следующему старту Антон восстановится и мы продолжим. А вот сейчас, если снимемся, сезон потеряем. Выручи, пожалуйста.
btn dance, «Ну ты, барин, задачи ставишь!». Ладно. Попробуем.
btn margo, Рит, я не зелёненький. Сегодня — скорее, синеватый. И ничего не соображаю, тем более в танцах!
end

:margo
prep=1
pln Вы сказали, что ничего не смыслите в танцах, но это не совсем так. В детстве вы около года занимались в танцевальной студии и неплохо вальсировали. С латиноамериканским репертуаром у вас не ладилось, но в программе соревнований его и не было. После недолгих уговоров вы согласились. За несколько часов Рите удалось растолковать вам вашу роль.</br></br> Вы уединились в пустой лаборатории кафедры ЭиС, куда после ремонта ещё не вернули мебель, и самоотверженно повторяли шаги по сто раз, пока не достигли автоматизма. После репетиции Рита заварила каркаде, и за неспешным разговором вы просидели еще минут сорок — отдохнули, восстановились.
btn dance, Идти танцевать
end

:dance
score=0
if prep=0 then pln Что будет на соревнованиям вы представляли смутно. Костюм Антона вам не подошел. На подбор и глажку своих вещей ушло столько времени, что вы опоздали на репетицию.</br></br>Коробова отвела вас в пустую лабораторию кафедры ЭиС, где после ремонта ещё не было мебели. Рита объясняла каждое движение, но к вечеру у вас в голове всё перемешалось. Вдобавок, из бальной программы вы умели танцевать только вальс, а были еще фокстрот и квикстеп.  
else pln Перед выступлением вы успели принять душ, спокойно погладить костюм и собраться с мыслями. Благодаря усилиям партнёрши, слова «фокстрот» и «квикстеп» начали обретать для вас конкретный смысл. & pln
pln <h1>Вечером</h1>
pln 
pln Соревнования проходят в большом актовом зале. Раньше вы и не замечали, какой здесь потрясающий паркет! Ну да ладно. Уже в сборе несколько сотен зрителей и три десятка пар-участниц. Ваш дуэт регистрируют под номером «25». Вы стараетесь держаться непринуждённо. Берете партнёршу за руку, ведёте в центр зала и занимаете место для танца.
btn around, Посмотреть по сторонам
btn inspire, Улыбнуться Рите
btn insure, Переспросить насчёт движений
end

:insure
score=score-1
pln Вы решаете напоследок ещё раз вспомнить последовательность шагов — шушукаетесь с партнёршей и опаздываете к музыке. Начало выступления слегка скомкано.
btn expromt, Добавить экспрессии
btn steps, Отвлечься от мыслей о шагах
btn concentrate, Сосредоточиться на шагах
end

:inspire
score=score+1
pln Вы улыбаетесь Рите — это приободряет её и вас. Звучит музыка, и вы плавно вступаете. Неплохое начало.
btn expromt, Добавить экспрессии
btn steps, Отвлечься от мыслей о шагах
btn concentrate, Сосредоточиться на шагах
end

:steps
score=score+2
pln Вы стараетесь не думать о шагах и просто двигаться, следуя чувству танца, приобретённому на репетиции. Рита очень грамотно вас поддерживает, незаметно подсказывает, что делать. У вас получается! Так держать.
btn diffs, Продолжать
end

:expromt
score=score-1
pln Вы решаете «выдать эмоцию»: растягиваете рот в широкой улыбке, буравите партнёршу страстным взглядом и подскакиваете на поворотах. Рита в недоумении, а что думают судьи, пока известно только им. В приступе экспрессии вы наступаете Рите на ногу. Неловкая заминка.
btn diffs, Продолжать
end

:concentrate
score=score-1
pln Вы думаете о шагах и вдруг... Не помните, что делать дальше. Ступор! На мгновение вы почти останавливаетесь. Рита тянет вас за собой и подсказывает следующие шаги. Вы кое-как продолжаете, но ошибка не ускользнёт от судей.
btn diffs, Далее
end

:around
score=score-1
pln Вы озираетесь по сторонам и замечаете, что два каких-то придурка строят вам рожи из зрительного зала. Неприятно. Ой! Уже музыка, и вы не сразу попадаете в такт. Раз, два... Вы снова лицом к залу, снова видите наглецов — они показывают вам неприличный жест. Вы отвлекаетесь — думаете, что сделаете с ними после выступления. Это не добавляет танцу органичности.
btn diffs, Далее
end

:diffs
pln Где-то близко слышен шелест. Вы не знаете, в чём дело, но ближайшая пара с удивлением смотрит в вашу сторону. Продолжаете танец, видите, что Рита волнуется, и на повороте понимаете... Номер, который был приколот к спине вашей рубашки, валяется на полу, затоптанный. Что делать?
btn dontmention, Не реагировать
btn encourage, Поддержать Риту
btn stop, Остановиться
end

:dontmention
pln Вы стараетесь продолжать как ни в чём не бывало, но получается неубедительно. Рита явно растеряна,  вы — ещё больше. Завершаете танец без ошибок, но и без настроения.
btn diffs2, Далее
end

:encourage
score=score+2
pln Вы дружески подмигиваете партнёрше: «Все хорошо». Результаты соревнований вас мало волнуют, но, раз взялись помочь, надо сделать всё возможное. Эта мысль помогает собраться и пройти оставшуюся часть вальса на хорошем уровне.
btn diffs2, Далее
end

:stop
score=score-1
pln Вы посмотрели в сторону судей и были готовы подать им знак, что надо остановить музыку, разобраться с номером, а потом продолжить танец с того же места. Рита угадала ваше намерение и потянула вас за собой:
pln — Ты что?! Не останавливайся. Надо хотя бы попытаться!
pln Вам неловко, да и судьи учтут заминку.
btn diffs2, Далее
end

:diffs2
pln Следующий танец — квикстеп. Звучит музыка, вы вступаете. Женский голос кричит из зала: «Двадцать пятый! Жги!»       
btn quick_1, Слушать музыку
btn quick_2, Прибавить скорости
btn quick_3, Не реагировать
end

:quick_1
score=score+1
pln Чтобы сохранить концентрацию, вы переводите своё внимание на музыку. Это помогает отстраниться от зала и выложиться по полной.
btn diffs3, Продолжать
end

:quick_2
score=score-1
pln Вы чуть ускоряетесь. Марго поддерживает новый темп, но через полминуты вы чувствуете, что работаете на пределе и вот-вот собьетесь. Ваши движения становятся нервозными. Все-таки для импровизации вам не хватает опыта.
btn diffs3, Продолжать
end

:quick_3
pln Вы стараетесь игнорировать зал и продолжаете двигаться в заданном темпе. Вроде бы все идет неплохо, но как-то без огонька.
btn diffs3, Продолжать
end

:diffs3
pln Последний танец. Быстрый фокстрот!
btn foxtrot_1, Подмигнуть судьям
btn foxtrot_2, Держаться ближе к центру площадки
btn foxtrot_3, Следить за плавностью движений
end

:foxtrot_1
pln Вновь звучит ритмичная музыка. Вас охватывает веселье: осталось последнее усилие. Вы встречаетесь взглядом с одной из судей — длинноволосой женщиной чуть за сорок. Подмигиваете ей, она улыбается.
btn final, Продолжить
end

:foxtrot_2
score=score-1
pln Вы перемещаетесь к центру зала и оказываетесь в «кольце» других пар. Тесновато.
pln — Ты куда? — тихо негодует Рита — Тут не развернуться!
btn final, Продолжить
end

:foxtrot_3
score=score+1
pln Вы скользите на длинных шагах, как корабль по волнам.
btn final, Продолжить
end

:final
pln Итак, соревнование завершено. Зрители горячо аплодируют, выкрикивают номера любимых пар. Вы замечаете плакатик, на котором ваши с Ритой имена. Приятно же! После нескольких томительных минут жюри объявляет результаты.
btn final_result, Слушать
end

:final_result
music data/mus/TheFinalTitles.mp3
pln <h1>Итоги</h1>
if score<3 then pln Попасть в призёры вам не удалось. Вы чувствуете себя немного виноватым, и всё же вы танцевали сегодня с замечательной партнёршей и честно старались. Рита с грустинкой в глазах треплет вас по плечу и просит не расстраиваться. Главное, что сезон для нее продолжается. & pln Через пятнадцать минут, по пути в свою комнату, вы с гордостью думаете о главной победе — вступлении в тайное общество. & btn triumph_or_victory, Всё равно неплохо!
if score>2 then pln Вы заняли почётное третье место! Невероятно! Вы с Маргаритой прыгаете от радости и обнимаетесь — друг с другом, друзьями-болельщиками и со всеми вокруг. Антон, который следил за выступлением, подходит, хромая, и жмёт вам руку. Организаторы вручают вам бронзовую медаль, на которой выгравирована эмблема соревнований — три паркетины. Это триумф! & pln По дороге в свою комнату вы думаете, как отметите это достижение в тайном студенческом обществе. & btn triumph_or_victory, Ура!
end

:triumph_or_victory
cls
if score>2 then pln <h2>ПОЛНАЯ ПОБЕДА!</h2> Вы всего второй день в подполье, а уже приобрели интересных друзей. Антон позвал вас в свой проект по разработке программного обеспечения для квадрокоптеров. Рита всем говорит, что вы герой и её спаситель. Ритины знакомые пригласили  вас работать на институтское радио «МайТех». Яркий танцевальный дебют привлек к вам внимание девчонок. И даже библиотекарь Мария Константиновна предложила обращаться к ней за библиографическими справками. Немногие подпольщики начинали с такого успеха. & image="data/img/Winner.png" else pln <h3>Победа!</h3> С первого дня в подполье вы приобрели репутацию весёлого и рискового парня. Вам даже поручили создать суфлёрский штаб при «Службе спасения на экзаменах». Но это уже другая история. & image="data/img/Win.png"
end

:telephone
if patience=0 then goto eend
if busy=0 then pln Вы подходите к телефону и берёте трубку. Слышен длинный гудок.
pln Наберете номер?
input phone_number & goto tn_entered
end

:tn_entered
if patience=0 then goto eend
busy=1
if phone_number="407183" or phone_number="407-18-3" or phone_number="407 18 3" then phone_right=1 else phone_right=0
if phone_right=0 then pln Вы не собираетесь тратить время на телефонное хулиганство.
if phoned=1 and phone_right=1 then pln Вы снова набираете номер и слышите последовательность «5832146».
if phoned=0 and phone_right=1 then phoned=1 & pln Вы набираете номер и ждёте ответа. На том конце снимают трубку. Вы по привычке здороваетесь, но тут же понимаете, что «позвонили в автоматическую сервисную службу Подполья». & pln — Запишите числовую последовательность: 5832146. Приятного дня! & pln Связь обрывается.
btn telephone, Попробовать другой номер
btn phone, Повесить трубку
end

:exit
if patience=0 then goto eend
just=0
pln Зачем идти на улицу, тем более без пальто? Дел невпроворот.
btn stands, Далее
end

:stands
if patience=0 then goto eend
pln <h3>Вестибюль</h3>
if just=1 then pln Вы прошли в мраморный вестибюль.  else p Вы в мраморном вестибюле. 
if been_to_entrance=0 then p Здесь холодно, потому что большие двери главного входа всё время открываются и закрываются, а на дворе январь. Новенькие турникеты мигают зелёными огоньками, впуская входящих. В гардеробе моют пол — туда не пройти. Время от времени с улицы вбегают продрогшие ребята, которые ходили курить без верхней одежды. & been_to_entrance=1
if inv_Карточка=0 then pln У стены — стойки с институтской газетой и буклетами для абитуриентов. & btn broshoure, К стойке с буклетами
if met_steph=0 then pln Возле кофейного автомата топчется ваш хороший знакомый, Стефан — программист из Швейцарии. else pln Стефан уже ушёл. 
btn exit, К выходу
if met_steph=0 then btn stephan, К Стефану
btn firstfloor, Назад в коридор
end

:broshoure
if patience=0 then goto eend
just=0
pln Вы подходите к стойке с буклетами. На обложке одного из них замечаете галочку маркером. Берёте этот экземпляр полистать.
pln 
proc contents
end

:contents
if patience=0 then goto eend
pln Белые глянцевые страницы приятно пахнут типографской краской.
pln Содержание:
btn pp, Цели и задачи учебного центра — стр. 1
btn pp, Учебные программы — стр.  2
btn pp, Порядок зачисления — стр. 3
btn pp, История МайТИ — стр. 4
btn graduates, Наши выпускники — стр. 5
btn stands, Закрыть буклет
end

:pp
if patience=0 then goto eend
cls
ltb=ltb+1
if ltb=1 then pln Всё это вы читали, когда с трепетом абитуриента сами листали похожий буклет. Здешняя жизнь представлялась вам бесконечно прекрасной.
if ltb=2 then pln Любопытно, но не более.
if ltb=3 then pln Да-да. Читали. Знаем.
if ltb=4 then pln Может, тут и есть что новое, но углубиться в это лучше на досуге.
if ltb=5 then pln Всё это занимательно, но...
if ltb=6 then pln Это вы уже читали.
if ltb=7 then pln Всё читано.
if ltb>7 then pln Сколько можно? Одно и то же!
pln 
proc contents
end

:graduates
if patience=0 then goto eend
inv+ Карточка
pln Вы открываете страницу под заголовком «Лучшие выпускники» и на развороте находите пластиковую карточку. Обыкновенная студенческая карта, как у любого здесь. Вы должны были её найти, не зря же на буклете пометка. Прекрасно. Теперь бы активировать её и понять, зачем она нужна.
btn stands, Далее
end

:stephan
if patience=0 then goto eend
image="data/img/Stephan.png"
just=0
if spoke_stephan=0 then pln Вы оба рады встрече. После рукопожатия ищете тему для разговора. & pln & pln <strong>Стефан:</strong> Ты фступаешь ф общестфо? & pln — Прямо сейчас прохожу испытание. & pln — Получается? & pln — Пока одни вопросы. Даже задание не сказали. & pln Стефан улыбается. & pln — Подсказыфать не могу, иначе нечестно будет. & pln — Да я понимаю. Ну, а про «загадайку» этого года что-нибудь знаешь? & pln — Немногое. Только... Он прекрасный математик, уфлекается поэзией, бретонским языком, афиамоделированием. & pln — Хоть что-то. Спасибо. & spoke_stephan=1 else pln Вы разговариваете со Стефаном.
if show_pass=1 then btn pass_request, Спросить пароль от компьютера
btn stands, Далее
end

:pass_request
if patience=0 then goto eend
if count_pass_request>0 then met_steph=1
image="data/img/Stephan.png"
pln — Ты знаешь пароль от компьютера в библиотеке?
pln — Разфе там есть парол?
pln — А кто администратор, не знаешь?
pln — Мы же догофорились...
pln — Ладно-ладно. Вопросов больше нет.
btn stands, Далее
end

:use_Калькулятор
if patience=0 then goto eend
pln
pln <i>Сломанный старенький калькулятор. Боковая поверхность залеплена изолентой. Похоже, его разбирали.</i>
end

:use_Калькулятор_Считать
if patience=0 then goto eend
camefrom=current_loc
goto count_wrong
end

:summ
if patience=0 then goto eend
arga=0
argb=0
pln Введите первое слагаемое.
input arga
pln > #arga$.
pln Введите второе слагаемое.
input argb
pln > #argb$.
summ_res=(#arga$+#argb$)*58
pln Результат: #summ_res$
btn count_wrong, Считать другие числа
end

:subt
if patience=0 then goto eend
pln Введите уменьшаемое.
input arga
pln > #arga$.
pln Введите вычитаемое.
input argb
pln > #argb$.
subt_res=(#arga$-#argb$)*17
pln Результат: #subt_res$
btn count_wrong, Считать другие числа
end

:mult
if patience=0 then goto eend
pln Введите первый множитель.
input arga
pln > #arga$.
pln Введите второй множитель.
input argb
pln > #argb$.
mult_res=#arga$*#argb$*21
pln Результат: #mult_res$
btn count_wrong, Считать другие числа
end

:part
if patience=0 then goto eend
pln Введите делимое.
input arga
pln > #arga$.
pln Введите делитель.
input argb
pln > #argb$.
mult_res=(#arga$/#argb$*2)+12
if mult_res=7320 then pln Результат: E#mult_res$ else pln Результат: #mult_res$
btn count_wrong, Считать другие числа
end

:count_wrong
if patience=0 then goto eend
cls
forget_procs
pln Вы пробуете считать на сломанном калькуляторе.
btn summ, Сложить два числа
btn subt, Вычесть из одного числа другое
btn mult, Перемножить два числа
btn part, Разделить одно число на другое
btn #%camefrom$, Убрать калькулятор в карман
end

:lib
librar_talk=0
if patience=0 then goto eend
pln <h3>Библиотека</h3>
p Просторная комната в бледно-жёлтых тонах. 
if lib_visited=0 then p Стены украшены портретами: Ломоносов, Менделеев, Циолковский, Гаусс, Эйлер, Нильс Бор, Зинаида Ермольева... & lib_visited=1 
pln Справа от входа — читательские рабочие места с компьютерами. Прямо — стойка библиотекаря, за которой начинаются стеллажи книг.
btn computers, К компьютерам
btn bookshelves, К стеллажам
btn librarian, К библиотекарю
btn firstfloor, Выйти в коридор
end

:bookshelves
if patience=0 then goto eend
pln Вы идёте по аллее книжных полок, где царит идеальный порядок. Книги разделены по категориям: учебная литература, художественная, справочная и периодика. Что же посмотреть?
btn art, Художественная
btn ed, Учебная
btn inf, Справочная
btn new, Периодика
btn lib, К выходу
end

:book
if patience=0 then goto eend
image="data/img/Librarian.png"
librar_talk=1
book_discussed=1
pln <strong>Библиотекарь</strong>: Ваш читательский билет, пожалуйста.
if inv_Карточка>0 then pln Вы на мгновение теряетесь, затем протягиваете найденную студкарту. else goto nolib_card
pln — Что вас интересует? Помочь с выбором?
pln — Спасибо, я люблю искать сам. У меня сразу несколько тем на примете.
pln — Ищите, пожалуйста — указывает на стеллажи. — Будут вопросы — обращайтесь.
btn librarian, Продолжить беседу
btn lib, Свернуть беседу
end

:nolib_card
if patience=0 then goto eend
librar_talk=1
pln — Ой... Студкарту забыл, извините... А если не на вынос, а тут посмотрю книги, можно?
pln — Да, конечно.
btn librarian, Продолжить беседу
btn lib, Свернуть беседу
end

:acg
if patience=0 then goto eend
image="data/img/Librarian.png"
librar_talk=1
if count_acg>1 then goto acg_visited
pln — Скажите, пожалуйста... Рядом с компьютером, о котором я говорил, лежит книга. Не библиотечная. Её оставил администратор?
pln Библиотекарь многозначительно улыбается. Похоже, её предупредили на ваш счёт.
pln — Возможно.
btn librarian, Продолжить беседу
btn lib, Свернуть беседу
end

:acg_visited
if patience=0 then goto eend
pln Она не скажет ничего нового. Книгу оставил администратор, и явно не случайно.
btn librarian, Продолжить беседу
btn lib, Свернуть беседу
end

:cg
if patience=0 then goto eend
librar_talk=1
admin=1
if count_cg>1 then goto cg_visited
image="data/img/Librarian.png"
pln — Вы не подскажете пароль от вон того компьютера? — показываете какого.
pln — Нет, к сожалению. Пароль ставил администратор. Это его рабочее место.
pln — Гм... А где найти администратора?
pln — Это наш студент, но где он сегодня — не могу сказать. Если нужен компьютер, попробуйте соседние, на них пароля нет.
pln — Угу.
btn librarian, Продолжить беседу
btn lib, Свернуть беседу
end

:cg_visited
if patience=0 then goto eend
image="data/img/Librarian.png"
pln Она не знает пароля.
btn librarian, Продолжить беседу
btn lib, Свернуть беседу
end

:librarian
if patience=0 then goto eend
image="data/img/Librarian.png"
if librar_talk=0 then p Библиотекарь —  приветливая пожилая женщина с аккуратной стрижкой и массивными бусами. else pln Вы разговариваете с библиотекарем.
if librar_talk=0 and count_librarian<2 then p  Она здоровается и ждет, что вы скажете. 
if book_discussed=0 then btn book, Попросить книгу
if show_pass=1 then btn cg, Спросить пароль от компьютера
if admin=1 and brb_look=1 then btn acg, Спросить об администраторе
btn lib, Ничего не спрашивать
end

:art
if patience=0 then goto eend
if artb=0 then pln Вы просматриваете раздел художественной литературы. Здесь море достойных книг, но пока вас занимает другое. Лучше заглянуть сюда на досуге. & artb=1 else pln У вас ещё будет время сюда зайти.
btn ed, Учебная
btn inf, Справочная
btn new, Периодика
btn lib, Идти к выходу
end

:ed
if patience=0 then goto eend
pln Учебники. Много... А вам и своих хватает.
btn art, Художественная
btn inf, Справочная
btn new, Периодика
btn lib, Идти к выходу
end

:inf
if patience=0 then goto eend
if brb_look=0 then pln Здесь собраны словари и справочники по любым предметам. else goto finddic
btn art, Художественная
btn ed, Учебная
btn new, Периодика
btn lib, Идти к выходу
end

:finddic
if patience=0 then goto eend
if dicfound=0 then pln Просматривая справочную литературу, вы замечаете бретоно-русский словарь. & dicfound=1 else pln Здесь, среди прочей справочной литературы, стоит бретоно-русский словарь.
btn dic, Взять словарь
btn art, Художественная
btn ed, Учебная
btn new, Периодика
btn lib, Идти к выходу
end

:new
if patience=0 then goto eend
p Газеты, журналы... 
if spoke_stephan=1 and journal_n=0 then p Ваше внимание привлекает подборка журнала «Авиамоделирование». & btn aviamodels, Читать свежий номер журнала else p Тут и свежие выпуски, и подборки номеров за 1990-е и 2000-е.   
btn art, Художественная
btn ed, Учебная
btn inf, Справочная
btn lib, Идти к выходу
end

:aviamodels
if patience=0 then goto eend
journal_n=1
pln Тут много схем и красочных иллюстраций. Но главное — из журнала торчит листок формата A4. Вы берете его и идёте к одному из офисных столов. Садитесь в кресло — кладёте листок перед собой. 
btn crypta, Далее
end

:crypta
if patience=0 then goto eend
pln <h3>Листок A4</h3>
pln <p>Надпись синим карандашом:</p>
pln
pln <i><strong>Крипта<u>рифм</u></strong></i>
pln
pln <i>Простая схема: цифры</br>Мои ищи.</br>Не ты хозяин шифра,</br>Но ты возьми ключи.</i>
pln
pln          XXX
pln            XX
pln        XXXX
pln        XXXX
pln        -----—
pln               ?                                       
pln
pln <i>P.S. Выкинь одинаковое, перемножь в уме или на листочке, что осталось, к полученному припиши два нуля и используй семь цифр.</i>
btn breton_computer, Перестать разглядывать листок
end

:dic
if patience=0 then goto eend
p Берёте с полки словарь и листаете его. На некоторых страницах попадатются карандашные надписи  
if messages=1 then p уже знакомым 
p мелким почерком.
btn lad, Смотреть слова на букву...
btn inf, Вернуть на место
end

:lad
if patience=0 then goto eend
pln В начале словаря — бретонский алфавит. В алфавите 25 букв, и он немного отличается от латинского:
pln 
pln          A, B, Ch, C'h, D, E, F, G, H, I, J, 
pln      K, L, M, N, O, P, R, S, T, U, V, W, Y, Z.
pln
pln Выберите интересующую букву и открывайте нужную страницу. 
input letter
pln 
if letter!="a" and letter!="b" and letter!="ch" and letter!="c'h" and letter!="d" and letter!="e" and letter!="f" and letter!="g" and letter!="h" and letter!="i" and letter!="j" and letter!="k" and letter!="l" and letter!="m" and letter!="n" and letter!="o" and letter!="p" and letter!="r" and letter!="s" and letter!="t" and letter!="u" and letter!="v" and letter!="w" and letter!="y" and letter!="z" then p Такой буквы в бретонском алфавите нет. else p Слова на букву & proc #%letter$
btn lad, Смотреть другую букву
btn inf, Вернуть словарь на полку
end

:a
if patience=0 then goto eend
p A начинаются сразу после вступительных статей, на странице 15.
end

:b
if patience=0 then goto eend
p B начинаются на странице 26.
end

:ch
if patience=0 then goto eend
p Ch начинаются на странице 38.
end

:c'h
if patience=0 then goto eend
p C'h начинаются на странице 45.
end

:d
if patience=0 then goto eend
p D начинаются на странице 64.
end

:e
if patience=0 then goto eend
p E начинаются на странице 78.
end

:f
if patience=0 then goto eend
p F начинаются на странице 86.
end

:g
if patience=0 then goto eend
p G начинаются на странице 99.
end

:h
if patience=0 then goto eend
p H начинаются на странице 105.
end

:i
if patience=0 then goto eend
p I начинаются на странице 113.
end

:j
if patience=0 then goto eend
p J начинаются на странице 123.
end

:k
if patience=0 then goto eend
p K начинаются на странице 124.
end

:l
if patience=0 then goto eend
p L начинаются на странице 125.
end

:m
if patience=0 then goto eend
p M начинаются на странице 132.
end

:n
if patience=0 then goto eend
p N начинаются на странице 141.
end

:o
if patience=0 then goto eend
p O начинаются на странице 145.
end

:p
if patience=0 then goto eend
p P начинаются на странице 151.
end

:r
if patience=0 then goto eend
p R начинаются на странице 167.
end

:s
if patience=0 then goto eend
p на букву S начинаются на странице 180.
end

:t
if patience=0 then goto eend
p T начинаются на странице 208.
end

:u
if patience=0 then goto eend
p U начинаются на странице 218.
end

:v
if patience=0 then goto eend
p V начинаются на странице 222.
end

:w
if patience=0 then goto eend
p W начинаются на странице 225.
end

:y
if patience=0 then goto eend
p Y (их мало) начинаются на последней странице 232.
end

:z
if patience=0 then goto eend
p Z начинаются на странице 232.
end

:computers
if patience=0 then goto eend
brett=0
p Тут пять рабочих мест: офисные столы с новенькими ЖК-мониторами и невысокие компьютерные кресла на колесиках. На столах — чистота и порядок. Только на одном лежит 
if brb_look=0 then p какая-то книга. & btn breton_computer_table, К столу, где книга else p учебник бретонского языка. & btn breton_computer_table, К столу, где  учебник
btn lib, К выходу
end

:breton_computer_table
if patience=0 then goto eend
pln Это деревянный письменный стол с двумя выдвижными ящиками. Вы сели в кресло — перед глазами монитор. В правом-верхнем углу экрана приклеен стикер с мелкой неразборчивой надписью. Монитор выключен. 
p  Справа от вас на столе лежит 
if brb_look=0 then p какая-то книга. else p учебник бретонского языка.
if brpc=0 then btn breton_computer_on, Включить компьютер else btn breton_computer_on, К компьютеру
if journal_n=1 then btn сrypto, Читать листок с криптарифмом
btn message, Прочесть надпись на стикере
if brb_look=0 then btn bretonbook, Осмотреть книгу else btn breton_book, Читать книгу
btn drawers, Порыться в ящиках
btn computers, Встать из-за стола
end

:breton_computer
if patience=0 then goto eend
p Вы за офисным столом. 
if brpc=1 then p Перед вами включенный экран с прилепленным в углу стикером.  else p Перед вами выключенный экран с прилепленным в углу стикером. 
p Справа от вас на столе лежит 
if brb_look=0 then p какая-то книга. else p учебник бретонского языка.
if journal_n=1 then p Тут же — листок с криптарифмом.
if brpc=0 then btn breton_computer_on, Включить компьютер else btn breton_computer_on, К компьютеру
if journal_n=1 then btn crypta, Читать листок с криптарифмом
btn message, Прочесть надпись на стикере
if brb_look=0 then btn bretonbook, Осмотреть книгу else btn breton_book, Читать книгу
btn drawers, Порыться в ящиках
btn computers, Встать из-за стола
end

:message
if patience=0 then goto eend
messages=1
pln Вы напрягаете зрение и разбираете мелкий кривой почерк: «Чтобы включить, см. формулу». Вот и всё, что здесь написано.
if brpc=0 then btn breton_computer_on, Включить компьютер else btn breton_computer_on, К компьютеру
if journal_n=1 then btn crypta, Читать листок с криптарифмом
if brb_look=0 then btn bretonbook, Осмотреть книгу else btn breton_book, Читать учебник
btn drawers, Порыться в ящиках
btn computers, Встать из-за стола
end

:desktop
if patience=0 then goto eend
pln Вы видите пустой рабочий стол, на фоновой картинке — надпись крупным шрифтом: «Запишите числовую последовательность: 1753924». Больше ничего примечательного.
btn breton_computer, Далее
end

:breton_computer_on
if patience=0 then goto eend
show_pass=1
if pass_right=1 then goto desktop
if brpc=0 then brpc=1 & pln Вы заглянули под стол и нажали кнопку питания на системном блоке. Раздалось глухое гудение. Монитор ожил — показал экран загрузки BarbOS — институтского дистрибутива Linux. Вдруг выскочила табличка: «Введите пароль» и всё застопорилось. Можно только ввести пароль или отступить. else pln На экране отображается табличка с требованием ввести пароль. 
btn enter_pass, Ввести пароль
btn breton_computer, Назад
end

:enter_pass
if patience=0 then goto eend
pln Вы установили курсор в поле ввода
input pass
goto pass_entered
end

:pass_entered
if patience=0 then goto eend
if pass="E7320" then cls & pln <strong>Правильный пароль! Загрузка системы...</strong></br></br>Вы видите пустой рабочий стол, на фоновой картинке — крупная надпись: «Запишите числовую последовательность: 1753924».</br></br>В поисках ещё чего-нибудь интересного вы бродите по каталогам жёсткого диска, но находите лишь стандартный набор офисных и математических программ. & pass_right=1 else pln Ошибка! Пароль неверный.
btn breton_computer, Далее
end

:drawers
if patience=0 then goto eend
drawer=0
hide_use_Зеркало_Положить=1
pln Здесь два ящика, и оба не заперты.
btn drawer1, Выдвинуть верхний
btn drawer2, Выдвинуть нижний
btn breton_computer, Не трогать
end

:drawer1
if patience=0 then goto eend
drawer=1
if inv_Калькулятор>0 and puttd1=0 then pln Выдвигаете верхний ящик — тут пусто. На дне нарисована маркером стрелка, которая указывает то ли вперёд, то ли вверх. & hide_use_Зеркало_Положить=0
if puttd1=1 then pln Выдвигаете верхний ящик. В зеркале, которое тут лежит, отражается формула, нацарапанная на нижней поверхности крышки стола: «3(a+11)/b». & btn take_mirror, Забрать зеркало
if inv_Калькулятор=0 then pln Выдвинув верхний ящик, вы находите старенький калькулятор.  Видно, что его разбирали: боковая поверхность заклеена изолентой. Вы пробуете считать и убеждаетесь, что калькулятор сломан — он выдаёт неправильные результаты. Любопытно! Вы кладёте находку в карман.</br></br>Ещё вы замечаете, что на дне уже пустого выдвинутого ящика нарисована маркером стрелка, которая указывает то ли вперёд, то ли вверх. & inv+ Калькулятор
if fcd1=0 then btn drawers, Далее & fcd1=1 else btn drawers, Закрыть
end

:drawer2
if patience=0 then goto eend
drawer=2
if puttd2=0 then pln Вы выдвигаете нижний ящик. Он пуст. & hide_use_Зеркало_Положить=0 else pln Вы выдвигаете нижний ящик. Здесь лежит зеркало. В нём отражается дно верхнего ящика. & btn take_mirror, Забрать зеркало
btn drawers, Закрыть
end

:use_Зеркало
if patience=0 then goto eend
pln
pln <i>Прямоугольное зеркало, где-то 6x7 см, без рамки. Вы очень хорошо запомнили хитрый взгляд, с которым Гриня протянул вам зеркало и сказал: «Вот. Положи куда-нибудь».</i>
pln
end

:use_Зеркало_Положить
if patience=0 then goto eend
pln
cls
if drawer=1 then inv- Зеркало & puttd1=1 & pln Вы кладёте зеркало в верхний ящик и видите отражение надписи, нацарапанной на нижней поверхности крышки стола. Это формула: «3(a+11)/b».
if drawer=2 then inv- Зеркало & puttd2=1 & pln Вы кладёте зеркало в нижний ящик. В зеркале отражается дно верхнего ящика.
btn take_mirror, Забрать зеркало
btn drawers, Закрыть
end

:take_mirror
if patience=0 then goto eend
cls
pln Вы достаёте зеркало из ящика. & inv+ Зеркало & puttd1=0 & puttd2=0 & hide_use_Зеркало_Положить=1
btn drawers, Закрыть
end

:bretonbook
if patience=0 then goto eend
brb_look=1
brb_opened=0
pln «Учебник бретонского языка». Книга в твёрдом светло-сером переплёте.
btn brb_frontpage, Открыть на титульном листе
btn lesson1, Открыть на первом параграфе
btn breton_computer, Закрыть книгу
end

:breton_book
if patience=0 then goto eend
brb_opened=0
pln Вы ещё раз заглядываете в учебник.
btn brb_frontpage, Титульный лист
btn lesson1, Первый параграф
btn breton_computer, Оставить книгу в покое
end

:brb_frontpage
if patience=0 then goto eend
p <h3>Учебник бретонского</h3>
if brb_opened=0 and frontpage_read=0 then pln Вы пробегаете взглядом титульный лист. & pln «Издание адресовано учащимся школ и колледжей, абитуриентам, студентам, преподавателям (пропускаете несколько строк с перечислением) ...а также широкому кругу лиц, изучающих язык самостоятельно. Пособие содержит краткий обзор грамматики и лексики бретонского языка и задания для лучшего усвоения материала. Составитель — М.П. Заворуев. © Издательство МАНИФЕСТ, 2007 год». & pln & pln Вы ожидали увидеть на форзаце библиотечный штамп или карман для формуляра, но их нет. & pln & frontpage_read=1
pln Сюда вложена закладка-календарик с надписью «БРЕТАНЬ». Над некоторыми буквами этого слова вы замечаете пометки от руки: над «Б» — цифра 2, над «Т» — 6, над «Н» — 4. На свободной части фона, очень мелко, надпись: «Букв местами не меняйте, но порядок соблюдайте. Цифры к буквам примеряйте, снизу — столбики цепляйте». 
btn lesson1, Читать первый параграф
btn breton_computer, Закрыть книгу
end

:lesson1
if patience=0 then goto eend
brb_opened=1
if ft=0 then pln <h3>«Урок 1»</h3>Нижняя половина страницы заклеена прямоугольником белой бумаги, на которой напечатано: & pln 
pln <strong><i>Переведите на бретонский</i></strong>
pln 1) Она говоРит, что Мне Нужно больше занимаТься.
pln 2) ПоЧему Ты не смОтришь Центральное телевидение?
pln 3) Здание было посТроено в воСЕмнадцаТом веке.
pln 4) НЕ забудЬ зАписать адРес.
pln 5) Он Ежедневно проходит мимо домА Сьё по пути нА работу.
pln 6) ВЫ нЕ поМожете мне закинутЬ чемодан на полку?
pln 7) Итоговое теСтирование проВоДится в новом здании Института.
pln
pln Хм. Явно сложноватые предложения для первого урока. Зачем же их сюда приклеили? И почему заглавные буквы расставлены в произвольных местах?
pln
if ft=0 then pln Вы пролистали книгу до конца, но больше не заметили ничего особенного. & ft=1
if tp=0 then btn tirepatch, Снять наклейку
btn brb_frontpage, Читать титульный лист
btn breton_computer, Закрыть книгу
end

:tirepatch
if patience=0 then goto eend
tp=1
pln Вы осторожно поддели ногтем уголок наклейки и попытались за него потянуть, но тут же поняли, что бумага слишком тонкая и увидеть исходного текста не удастся, к тому же, есть риск повредить саму наклейку.
btn lesson1, Прекратить
end

:use_Тамагочи
if patience=0 then goto eend
pln </br></br><i>У вас в руках электронная игрушка: сиреневый пластиковый брелок овальной формы, с экраном и четырьмя кнопками.</i>
end

:use_Тамагочи_Играть
if patience=0 then goto eend
cls
player_location=current_loc
if egg_activated=0 then goto egg_activation else goto egg
end

:egg
cls
clsb
image="data/img/Egg.png"
hide_use_Тамагочи_Статистика_вкл-выкл=0
if egg_activated=0 then goto egg_activation
if pin_mode_on=1 then goto pin_mode
pln На экране — виртуальный питомец, похожий на медведя. Кнопки под экраном подписаны символами.
proc egg_btns
end

:egg_btns
btn o_sym, o
btn select, -<
btn x_sym, +
btn light_off, c
btn cls & o_pressed=0 & goto #%player_location$, Убрать игрушку в карман
end

:egg_btns_pin
if result_announced=0 then proc egg_pin_indicator else result_announced=0
if pin_position<4 then pin_position=pin_position+1 else goto egg_result
btn o_sym_pin, 1. o
btn select_pin, 2. -<
btn x_sym_pin, 3. +
btn light_off_pin, 4. c
btn #%player_location$, Убрать игрушку в карман
end

:egg_result
result_announced=1
if pin1_value="метла" and pin2_value="луна" and pin3_value="крест" and pin4_value="яблоко" then pin_entered=1 & cls & clsb & pln На экране тамагочи появляется сообщение: & pln <h1>«Правильно! Запись активирована».</h1> & pln После секундной паузы — другая надпись: «Ждите...». Через минуту к вам уже спешат Гриня, Стефан и какие-то незнакомые ребята, которые решили вас поздравить. & btn win, Вы справились else pin1_value=0 & pin2_value=0 & pin3_value=0 & pin4_value=0 & pin_position=0 & pln & pln <strong>Экран на секунду пустеет, затем в центре его снова появляется строка из четырёх точек. </br>Все значки исчезли: 0, 0, 0, 0</strong>. &  proc egg_btns_pin
end

:use_Тамагочи_Статистика_вкл-выкл
if pin_mode_on=0 and egg_stats_on=0 then egg_stats_on=1 & pln </br><i>Состояние питомца на контроле.</i></br> & proc stats_bar else egg_stats_on=0 & pln </br><i>Вы больше не следите за состоянием питомца.</i></br>
end

:pin_mode
if patience=0 then goto eend
hide_use_Тамагочи_Статистика_вкл-выкл=1
cls
clsb
pin1_value=0
pin2_value=0
pin3_value=0
pin4_value=0
pin_position=0
if been_here=0 then pln Вы три раза подряд нажали «o». Вместо медведя на экране появилась строка из четырёх точек. & been_here=1 else pln На экране — строка из четырёх точек. 
pln Первая точка ярко-чёрная, остальные — бледные, как тень.
proc egg_btns_pin
end

:o_sym
if patience=0 then goto eend
if asleep=1 then goto sleep_report
if o_pressed>1 or pin_mode_on=1 then o_pressed=0 & pin_mode_on=1 & goto pin_mode
p Вы нажали «o». 
if ill=1 then pln Медведь не будет есть — он болен. 
if fedup>0 and ill!=1 then p Мишка отрицательно помотал головой. Он не голоден. Сытость: #fedup$0%. 
if fedup<1 and ill!=1 then fedup=10 & hungry=0 & p Слева от питомца появилось яблоко. Медведь повернулся к нему — яблоко стало убывать: появился надкус с одной стороны, с другой, вот оно превратилось в огрызок и, наконец, исчезло. Мишка хлопнул себя по животу — сытость 100%. 
o_pressed=o_pressed+1
proc egg_btns
end

:o_sym_pin
if pin_position=1 then pin1_value="яблоко" & p Вместо первой точки загорается значок в виде яблока.
if pin_position=2 then pin2_value="яблоко" & p Вторая точка превращается в значок в виде яблока.
if pin_position=3 then pin3_value="яблоко" & p Третья точка превращается в значок в виде яблока.
if pin_position=4 then pin4_value="яблоко" & p Четвертая точка превращается в значок в виде яблока.
proc egg_btns_pin
end

:x_sym
if patience=0 then goto eend
o_pressed=0
if asleep=1 then goto sleep_report
p Вы жмёте «+». На экране рядом с мишкой появляется аптечка.
if ill=1 then health_granted=35  & ill=0 & p Она дрожит и эта дрожь передается медведю — идет лечение. Через секунду аптечка исчезает — питомец снова здоров! else p Но тут же исчезает — лечение не требуется.
proc egg_btns
end

:x_sym_pin
if pin_position=1 then pin1_value="крест" & pin4_value="крест" & p На месте первой и последней в ряду точек появляется по медицинскому кресту.
if pin_position=2 then pin2_value="крест" & pin1_value="крест" & p На месте второй и первой точек появляется по медицинскому кресту.
if pin_position=3 then pin3_value="крест" & if pin2_value<>"луна" then pin2_value="крест" &  p На месте третьей и второй точек появляется по медицинскому кресту. else  p На месте третьей точки появляется крест.
if pin_position=4 then pin4_value="крест" & pin3_value="крест" & p На месте четвертой и третьей точек появляется по медицинскому кресту.
proc egg_btns_pin
end

:select
if patience=0 then goto eend
o_pressed=0
if asleep=1 then goto sleep_report
p Вы жмёте «-<». На экране рядом с мишкой появляется метла. 
if dirty=1 then tidyness=25 & dirty=0 & p Несколько взмахов, и вокруг питомца  снова чисто. else p Она тут же исчезает. Убирать нечего.
proc egg_btns
end

:select_pin
if pin_position=1 then pin1_value="метла" & p На месте первой точки появляется значок «метла».
if pin_position=4 then pin4_value="метла" & p На месте последней в ряду точки — значок «метла». Значки с остальных точек исчезли. & pin1_value=0 & pin2_value=0 & pin3_value=0
if pin_position=3 then pin3_value="метла" & p На месте третьей слева точки появляется значок «метла». Значки с первой и второй точек исчезли. & pin1_value=0 & pin2_value=0
if pin_position=2 then pin2_value="метла" & p На месте второй слева точки появляется значок «метла», а значок с первой точки (#%pin1_value$) исчез. & pin1_value=0
proc egg_btns_pin
end

:light_off
if patience=0 then goto eend
o_pressed=0
if asleep=1 then goto sleep_report
if asleep=0 then p Вы жмёте «c». 
if gotosleep>1 and uptime>0 and fedup> 0 then pln Медведю надоело спать и вам не удаётся погасить свет.
if ill=0 and asleep=0 and gotosleep<2 and fedup>0 and tidyness>0 then p Экран чернеет — вы погасили свет. Мишка засыпает. & asleep=1 & sleeping=20 & uptime=0 & gotosleep=gotosleep+1
if fedup<1 and ill=0 then pln Мишка слишком голоден, чтобы спать.
if ill=1 then pln Мишка не заснёт — он болен.
if dirty=1 and ill=0 and fedup>0 then pln Мишка не может спать в грязи!
proc egg_btns
end

:light_off_pin
if pin_position=1 then pin1_value="луна" & p На месте первой точки появляется значок в виде луны на тёмном небе.
if pin_position=2 then pin2_value="луна" & p На месте второй слева точки появляется значок в виде луны на тёмном небе.  & if pin1_value<>"метла" then p Значок с первой точки (#%pin1_value$) перемещается на четвёртую. & pin4_value=pin1_value & pin1_value=0
if pin_position=3 then pin3_value="луна" & p Вместо третьей слева точки появляется значок в виде луны. & if pin2_value<>"метла" then p Значок со второй точки (#%pin2_value$) сдвинулся на первую, а тот, что был на первой (#%pin1_value$), исчез.  & pin1_value=pin2_value & pin2_value=0
if pin_position=4 then pin4_value="луна" & p На месте четвёртой точки появился значок в виде луны на темном небе. & if pin3_value<>"метла" then p Третий (#%pin3_value$) и второй (#%pin2_value$) значки сдвинулись на один влево, а тот, что был первым в ряду (#%pin1_value$) — исчез.  & pin1_value=pin2_value & pin2_value=pin3_value & pin3_value=0
proc egg_btns_pin
end

:sleep_report
pln <strong><i>Медведь спит.</i></strong>
if previous_loc="o_sym" then o_pressed=o_pressed+1
if o_pressed>2 then o_pressed=0 & pin_mode_on=1 & goto pin_mode else proc egg_btns
end

:eend
cls
music data/mus/Lost.mp3
clsb
pln Слышен протяжный писк! Вы смотрите на тамагочи. Мишка берет узелок через плечо и уходит с экрана.
pln
pln Вы пробуете жать на кнопки — выскакивает надпись:
pln «Ты дисквалифицирован за жестокость».
pln Попытка вступить в Подполье не удалась.
pln <br><br><h1>Конец истории</h1>
perkill
invkill
end

:egg_pin_indicator
if current_loc!="pin_mode" and pin_position!=4 then pln 
pln 
pln <strong>Введённые символы:</strong> #%pin1_value$, #%pin2_value$, #%pin3_value$, #%pin4_value$
end

:common
if patience=0 then goto eend
if pin_mode_on=0 and egg_stats_on=1 then proc stats_bar
if hungry=1 and pin_mode_on=0 then pln <strong><i>[Тамагочи] Медведь голоден.</i></strong></br></br>
if dirty=1 and pin_mode_on=0 then pln <strong><i>[Тамагочи] Пора убирать за питомцем.</i></strong></br></br>
if egg_activated=1 and asleep=0 and tidyness>0 then tidyness=tidyness-1
if tidyness=0 and dirty=0 and pin_mode_on=0 then dirty=1 & pln <i>Тамагочи пищит! Нужна уборка.</i></br></br>
if ill=1 and pin_mode_on=0 then pln <strong><i>[Тамагочи] Мишка болеет.</i></strong></br></br>
if egg_activated=1 and asleep=0 and health_granted>0 and ill=0 then health_granted=health_granted-1
if asleep=0 and ill=0 and health>28 and pin_mode_on=0 then ill=1 & pln <i>Тамагочи пищит. Медведь заболел!</i></br></br>
if egg_activated=1 and asleep=0 and health_granted<1 and ill=0 and pin_mode_on=0 then health=rnd30 else health=0
if pin_mode_on=0 and asleep=0 and egg_activated=1 and fedup<1 and current_loc!="o_sym" and hungry=0 then hungry=1 & pln <i>Вы слышите жалобный писк тамагочи. Мишка  голоден.</i></br></br>
if asleep=0 and egg_activated=1 and fedup>0 and pin_mode_on=0 then fedup=fedup-1
if egg_activated=1 and asleep=0 and gotosleep>1 and uptime<1 and pin_mode_on=0 then gotosleep=0.
if uptime>0 then uptime=uptime-1
if sleeping>0 and pin_mode_on=0 then asleep=1 & uptime=0 & sleeping=sleeping-1
if asleep=1 and sleeping<1 and pin_mode_on=0 then asleep=0 & sleeping=0 & uptime=25 & pln <strong><i>[Тамагочи] Медведь проснулся.</i></strong></br></br>
if ill=1 and dirty=1 and fedup=0 and patience>0 then patience=patience-1
end

:stats_bar
if ill=0 then healthy="да" else healthy="нет"
if dirty=0 then clean="да" else clean="нет"
pln <div class="feel" style="background: #7B68EE; padding: 5px; border: solid 1px black; float: left;"><strong>Питомец</strong> || Сытость: #fedup$0% | Чистота: #clean$ | Здоров: #healthy$</div></br></br>
end

:egg_activation
if patience=0 then goto eend
pln Тамагочи спит. Пока есть другие дела, лучше не будить, чтоб не беспокоил. Но если очень хочется, то...
btn egg_activated=1 & goto egg, Активировать
btn #%player_location$, Убрать игрушку в карман
end